allprojects {
    group 'com.zendesk.jazon'
}

subprojects {
    apply plugin: 'groovy'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourceJar, javadocJar
    }

    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    
    signing {
        required { !version.endsWith('SNAPSHOT') }
        sign publishing.publications
    }

    publishing {
        repositories {
            maven {
                String snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                String releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
                credentials {
                    username project.hasProperty('ossrhUsername') ? project['ossrhUsername'] : ''
                    password project.hasProperty('ossrhPassword') ? project['ossrhPassword'] : ''
                }
            }
        }
    }

    publish {
        doLast {
            println "The published version: ${version}"
        }
    }
}
